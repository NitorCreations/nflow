plugins {
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'de.undercouch.download' version '5.4.0'
	id 'java'
}

group = 'io.nflow.springboot.fullstack.maven'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
  nflowExplorer
}

dependencies {
	implementation 'io.nflow:nflow-rest-api-spring-web:10.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2:2.2.220'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	nflowExplorer group: 'io.nflow', name: 'nflow-explorer', version: '10.0.0', ext: 'tar.gz'
}

task resolveNflowExplorer(type: Copy) {
  destinationDir = file("$buildDir/resources/main/static/explorer")
  from { tarTree(resources.gzip(configurations.nflowExplorer.singleFile)) }
}

test {
	useJUnitPlatform()
}

processResources.dependsOn resolveNflowExplorer
