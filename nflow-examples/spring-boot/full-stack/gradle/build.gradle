plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'de.undercouch.download' version '4.1.1'
	id 'java'
}

group = 'io.nflow.springboot.fullstack.maven'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
  nflowExplorer
}

dependencies {
	implementation 'io.nflow:nflow-rest-api-spring-web:8.0.0-SNAPSHOT'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	nflowExplorer group: 'io.nflow', name: 'nflow-explorer', version: '8.0.0-SNAPSHOT', ext: 'tar.gz'
}

task resolveNflowExplorer(type: Copy) {
  destinationDir = file("$buildDir/resources/main/static/explorer")
  from { tarTree(resources.gzip(configurations.nflowExplorer.singleFile)) }
}

test {
	useJUnitPlatform()
}

processResources.dependsOn resolveNflowExplorer
